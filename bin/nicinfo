#!/usr/bin/perl

use strict;
use warnings;

use FindBin;

use lib "$FindBin::RealBin/../lib";
use Ceni::Backend;

use Data::Dumper;
use Getopt::Long;

my $dbg = 0;
my $ether;
my $help;
my $iface;
my $wifi;

my @i;
my %i;

my @f = ('connection_type', 'drivers', 'subsystems', 'address', 'desc');

GetOptions(
	'debug'    => \$dbg,
	'ethernet' => \$ether,
	'help'     => \$help,
	'iface=s'  => \$iface,
	'wifi'     => \$wifi,
);

if ($help) {
	print "Usage: nicinfo [ --ethernet ] [ --wifi ] [ --iface <iface> ]\n";
	exit 0;
}

my $bend = new Ceni::Backend({ debug => $dbg, });

$bend->nic_info;

if ($iface) {
	if ($bend->is_iface_valid($iface)) {
		@i = ($iface);
	}
	else {
		print "E: interface does not exist: $iface\n";
		exit 1;
	}
}
else {
	@i = sort keys %{ $bend->get_iface_info };
}

for my $if (@i) {
	my $nic = $bend->get_iface_info($if);

	if ($wifi) {
		$nic->{'connection_type'} eq 'wireless' or next;
	}
	elsif ($ether) {
		$nic->{'connection_type'} eq 'ethernet' or next;
	}

	$i{$if} = join(',', $if, map { $nic->{$_}; } @f);
}

for my $if (sort keys %i) {
	print $i{$if} . "\n";
}

__END__

=head1 NAME

nicinfo - print information about network interfaces

=head1 SYNOPSIS

  nicinfo [ --ethernet ] [ --wifi ] [ --iface <iface> ]

=head1 DESCRIPTION

This script detects network interfaces present in system and prints
out driver, interface type, bus, mac address and a description for
each interface.

=head1 OPTIONS

=over

=item B<--iface> <iface>

Detect and print information for the interface with kernel name <iface>.
Exmaples of <iface> are "eth0", "ath0" and "wlan0".

=item B<--ethernet>

Detect only ethernet devices, not wirless.

=item B<--wifi>

Detect only wireless devices, not ethernet.

=back

=head1 AUTHOR

Kel Modderman, E<lt>kel@otaku42.deE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2007 - 2010 by Kel Modderman

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation; either version 2
of the License, or (at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this package; if not, see <http://www.gnu.org/licenses>

On Debian GNU/Linux systems, the text of the GPL-2 license can be
found in /usr/share/common-licenses/GPL-2.

=cut
